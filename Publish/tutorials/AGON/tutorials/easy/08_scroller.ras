program Scroller;
@use "screen/screen"
@use "system/memory"
var
	scrollVal : byte=0;
	textPointer : ^byte;
	text : cstring = ("                                        ",
					"THIS IS A SAMPLE SCROLLER TEXT",
					 "                                        ");
	const kernal : byte = 1;
	
// Position of scroller
	const ypos : byte = 16;
// Position of scroller raster line
	const yposRaster:byte=$A0;

	ptr : ^byte;

/*
  The following method just copes the current contents of textPointer to screenmemory
*/
procedure PrintText();
begin
	ptr := #Screen::screen0 + ypos*Screen::width;
	Memory::Copy(textPointer, ptr, Screen::width-1);
end;

/*
 	The following method decreases the scroll value. If the value is zero, then
 	the scroll value is reset to 7, the text pointer is increased and a new
 	text is printed to the screen. 
*/
procedure UpdateScrollAndText();
begin
	// Scroll has looped 8 pixels
	if (scrollVal=0) then 
	begin
		// Reset scroll value
		scrollVal:=7; 
		// Increase text pointer
		textPointer+=1;
		// Reset scroller text if string termination "0" is encountered
		if (textPointer[39]=0) then textPointer:=#text;
		// Print new string to the screen
		PrintText();			
	end
	else dec(scrollVal);
end;


// Forward declare "Bottom"

interrupt Bottom();


interrupt Top();
begin
	StartIRQ(kernal);
	Screen::border:=red;
	Screen::background:=red;
	Screen::Scrollx(0); // make sure that everythingin the "TOP" layer doesn't scroll
	RasterIRQ(Bottom(),yposRaster,kernal); // bottom os referenced here, so it needs to be forward declared
	// Update text and etc from the "Top" layer
	UpdateScrollAndText();		
	CloseIRQ();
end;



interrupt Bottom();
begin
	StartIRQ(kernal);
	// Wait(3) makes sure that we set the background color when the raster is outside the screen,
	// removing any artifacts
	wait(3);
	Screen::border:=black;
	Screen::background:=black;
	// Scroll here!
	Screen::Scrollx(scrollVal);
	
	RasterIRQ(Top(),$00,kernal);
	CloseIRQ();
end;
/*
	Initializes the screen etc
*/
procedure InitScreen();
begin
	// Clear screen with spaces
	Screen::Clear(#Screen::screen0, key_space);
	// Fill the upper screen color ram with black
	Memory::Fill(#Screen::color, black, 0);
	// Fill the scroll area with light red
	Memory::Fill(#Screen::color + ypos*Screen::width,light_red,0);
	
	// Print some static text
	Screen::PrintString("THIS TEXT IS STATIONARY", 8,2,#Screen::Screen0);
	
	// Removes the 0th and 39th column on the screen, enabling the "smooth" transition at 
	// the edges. 
	Screen::HideBorders();
end;

begin
	InitScreen();
	// Let the text pointer point to the actual text
	textPointer:=#text;
	// Start the raster chain!
	StartRasterChain(Top(), 0,kernal);
	Loop(); 
end.
